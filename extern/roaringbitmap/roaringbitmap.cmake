include (FetchContent)

set (roaringbitmap_urls
    https://github.com/RoaringBitmap/CRoaring/archive/refs/tags/v3.0.1.tar.gz
)
vsag_resolve_thirdparty_override (ROARINGBITMAP v3.0.1 roaringbitmap_urls)
FetchContent_Declare (
    roaringbitmap
    URL ${roaringbitmap_urls}
    URL_HASH MD5=463db911f97d5da69393d4a3190f9201
    DOWNLOAD_NO_PROGRESS 0
    INACTIVITY_TIMEOUT 5
    # filesize ~= 90MiB
    TIMEOUT 90
)

set (ROARING_USE_CPM OFF)
set (ENABLE_ROARING_TESTS OFF)

if (DISABLE_AVX_FORCE OR NOT COMPILER_AVX_SUPPORTED)
  set(ROARING_DISABLE_AVX ON)
endif ()

if (DISABLE_AVX512_FORCE OR NOT COMPILER_AVX512_SUPPORTED)
  set (ROARING_DISABLE_AVX512 ON)
endif ()

# exclude roaringbitmap in vsag installation
FetchContent_GetProperties (roaringbitmap)
if (NOT roaringbitmap_POPULATED)
    FetchContent_Populate (roaringbitmap)
    add_subdirectory (${roaringbitmap_SOURCE_DIR} ${roaringbitmap_BINARY_DIR} EXCLUDE_FROM_ALL)
    target_compile_options (roaring PRIVATE -Wno-unused-function)
    # CRoaring 3.0.1 trips a false-positive -Wstringop-overflow in
    # run_container_offset() on GCC 12+, and CRoaring builds with -Werror.
    if (CMAKE_C_COMPILER_ID STREQUAL "GNU" AND
        CMAKE_C_COMPILER_VERSION VERSION_GREATER_EQUAL 12)
        target_compile_options (roaring PRIVATE -Wno-stringop-overflow)
    endif ()
endif ()

if (NOT TARGET vsag_roaring_headers)
    add_library (vsag_roaring_headers INTERFACE)
endif ()
target_include_directories (vsag_roaring_headers INTERFACE
    ${roaringbitmap_SOURCE_DIR}/include
    ${roaringbitmap_SOURCE_DIR}/cpp)
